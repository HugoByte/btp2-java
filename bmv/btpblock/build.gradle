version = '0.1.0'

dependencies {
    compileOnly("foundation.icon:javaee-api:$javaeeVersion")
    implementation("foundation.icon:javaee-scorex:$scorexVersion")
    implementation project(':lib')

    testImplementation("org.junit.jupiter:junit-jupiter-api:$jupiterVersion")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:$jupiterVersion")

    testImplementation("foundation.icon:javaee-unittest:$javaeeUnittestVersion")
    testImplementation project(':test-lib')

    testImplementation "foundation.icon:icon-sdk:$iconsdkVersion"
    testImplementation 'foundation.icon:javaee-integration-test:0.9.0'
}

optimizedJar {
    dependsOn(project(':lib').jar)
    mainClassName = 'foundation.icon.btp.bmv.btpblock.BTPMessageVerifier'
    archivesBaseName = 'bmv-btpblock'
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } { exclude "score/*" }
    enableDebug = debugJar
}

deployJar {
    endpoints {
        local {
            uri = scoreTest.url
            nid = scoreTest.parseNid(scoreTest.nid)
        }
    }
    keystore = scoreTest.default.keyStore
    password = scoreTest.default.resolvedKeyPassword
    parameters {
        [
                arg('srcNetworkID', 'btp://network.address'),
                arg('networkTypeID', '0x?'),
                arg('bmc', 'cx?'),
                arg('blockHeader', '0x?'),
                arg('seqOffset', '0x?')
        ]
    }
}

test {
    useJUnitPlatform {
        if (!integrationTest) {
            excludeTags("integration")
        } else {
            // use the common config files
            systemProperty('env.props', new File('src/test/resources/env.props'))

            def prefix = 'score.path.'
            systemProperty(prefix + 'bmv-' + project.name, optimizedJar.outputJarName)
            dependsOn optimizedJar
            systemProperty prefix + 'bmc-mock.scoreFilePath', tasks.getByPath(":test-lib:optimizedJarMockBMC").outputJarName
        }
    }
}
